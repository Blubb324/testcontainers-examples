buildscript {
   repositories {
      mavenCentral()
   }
   dependencies {
      classpath("org.springframework.boot:spring-boot-gradle-plugin:2.2.4.RELEASE")
   }
}

plugins {
   id "com.github.ben-manes.versions" version "0.27.0"
}

group 'tk.hildebrandt.testcontainers'

ext {
   junit4Version = '4.13'
   junit5Version = '5.6.2'
   testNgVersion = '7.3.0'

   testContainersVersion = '1.14.3'

   logbackVersion = '1.2.3'

//   springDataJdbcVersion='1.1.9.RELEASE'
   springDataJdbcVersion = '2.0.3.RELEASE'
   springBootVersion = '2.3.3.RELEASE'

   postgresJdbcDriverVersion = '42.2.15'
   mysqlJdbcDriverVersion = '8.0.21'
   h2JdbcDriverVersion = '1.4.200'
   commonsDbcpVersion = '2.7.0'

   groovyVersion = '2.5.13'
   gebVersion = '3.4'
   cucumberVersion = '6.5.0'
   cucumberGroovyVersion = '5.1.3'
   cucumberReportingVersion = '4.0.70'
   seleniumJavaVersion = '3.141.59'
   antVersion = '1.10.3'
   slf4jVersion = '1.7.30'
}

subprojects {
   apply plugin: 'java'

   sourceCompatibility = '1.8'
   targetCompatibility = '1.8'

   repositories {
      mavenCentral()
   }
   configurations.all {
      resolutionStrategy {
         failOnVersionConflict()

         force "org.springframework.data:spring-data-commons:${springBootVersion}",
            "org.springframework.data:spring-data-relational:${springDataJdbcVersion}",
            "org.slf4j:slf4j-api:${slf4jVersion}",
            "junit:junit:${junit4Version}",
            "org.junit.jupiter:junit-jupiter-api:${junit5Version}",
            "org.codehaus.groovy:groovy-all:${groovyVersion}",
            "io.cucumber:cucumber-core:${cucumberVersion}",
            "org.apache.ant:ant:${antVersion}",
            "org.apache.ant:ant-launcher:${antVersion}",
            "io.cucumber:messages:13.0.1",
            "org.apache.commons:commons-lang3:3.9",
            "com.google.guava:guava:27.1-jre",
            "org.testng:testng:${testNgVersion}",
            "org.checkerframework:checker-qual:3.5.0"

         eachDependency { DependencyResolveDetails details ->
            if (details.requested.group == 'org.testcontainers') {
               details.useVersion testContainersVersion
            }
            if (details.requested.group == 'org.springframework.boot' && details.requested.name.endsWith("-starter")) {
               details.useVersion springBootVersion
            }
         }
      }
   }
}
